#!/usr/bin/perl

use warnings;
use strict;

use ExtUtils::Miniperl  qw/writemain/;
use List::Util          qw/max/;

open my $MAIN, ">", \my $C;
select $MAIN;
writemain;

sub Cqq {
    local $_ = shift // $_;
    s/\\/\\\\/g;
    s/"/\\"/g;
    $_;
}

my $argc        = @ARGV + 1;
my $argv_len    = $argc + 1;
my $argv_buf    = join qq/\\0"\n"/, map Cqq, $0, @ARGV;
my $argv_init   = "my_argv[0] = my_argv_buf;\n";
my $ptr         = 1 + length $0;

for (1..@ARGV) {
    $argv_init .= "my_argv[$_] = my_argv_buf + $ptr;\n";
    $ptr += 1 + length $ARGV[$_ - 1];
}

$ptr++;

for ($C) {
    s{\*my_perl;\K}{
        static int my_argc = $argc;
        static char my_argv_buf[$ptr] = "$argv_buf\\0";
        static char *my_argv[$argv_len];
    };
    s{; \K ( [^;]* perl_parse\( [^)]* ,\s+) argc,\s+ argv,\s+}{
        $argv_init
        my_argv[$argc] = (char *)NULL;
        $1 my_argc, my_argv,
    }x;
}

select STDOUT;
print $C;
